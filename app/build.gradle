apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "com.polygraphene.alvr.ar"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode 12
        versionName "1.9.2"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags ""
            }
        }
        manifestPlaceholders = [filter:"android.intent.category.LAUNCHER"]
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            ndk {
                abiFilters "arm64-v8a", "armeabi-v7a"
            }
        }
        debug {
            ndk {
                abiFilters "arm64-v8a", "armeabi-v7a"
            }
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }

    }
    sourceSets {
        main {
            jniLibs.srcDirs = ["src/main/jniLibs"]
        }
    }
    // Remove osig file from release apk
    applicationVariants.all { variant ->
        if (variant.buildType.name == 'release') {
            variant.mergeAssets.doLast {
                delete(fileTree(dir: variant.mergeAssets.outputDir, includes: ['oculussig_*']))
            }
        }
    }
    flavorDimensions "distribute"
    productFlavors {
        Github {
            minSdkVersion 21
            applicationId 'com.polygraphene.alvr.ar'
            targetSdkVersion 26
            dimension "distribute"
            manifestPlaceholders = [filter:"android.intent.category.LAUNCHER"]
        }
        OculusStore {
            minSdkVersion 21
            applicationId 'com.polygraphene.alvr.ar.oculus'
            targetSdkVersion 26
            dimension "distribute"
            manifestPlaceholders = [filter:"android.intent.category.INFO"]
        }
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar','*.so'])
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'
    implementation 'com.google.ar:core:1.0.0'
    implementation "com.google.ar.sceneform.ux:sceneform-ux:1.0.0"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}
